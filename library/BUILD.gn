# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/flutter.gni")
import("//build/packaging.gni")
import("//library/engine.gni")

published_shared_library("flutter_embedder") {
  if (is_linux) {
    sources = [
      "shared/src/internal/engine_method_result.cc",
      "shared/src/internal/engine_method_result.h",
      "shared/src/internal/json_message_codec.cc",
      "shared/src/internal/json_message_codec.h",
      "shared/src/internal/keyboard_hook_handler.h",
      "shared/src/internal/plugin_handler.cc",
      "shared/src/internal/plugin_handler.h",
      "shared/src/internal/text_input_model.cc",
      "shared/src/internal/text_input_model.h",
      "shared/src/internal/text_input_plugin.cc",
      "shared/src/internal/text_input_plugin.h",
      "shared/src/embedder.cc",
      "shared/src/json_method_call.cc",
      "shared/src/json_method_codec.cc",
      "shared/src/json_plugin.cc",
      "shared/src/method_call.cc",
      "shared/src/method_channel.cc",
      "shared/src/method_codec.cc",
      "shared/src/method_result.cc",
      "shared/src/plugin.cc",
    ]
    public = [
      "shared/include/flutter_desktop_embedding/binary_messenger.h",
      "linux/include/flutter_desktop_embedding/embedder.h",
      "shared/include/flutter_desktop_embedding/json_method_call.h",
      "shared/include/flutter_desktop_embedding/json_method_codec.h",
      "shared/include/flutter_desktop_embedding/json_plugin.h",
      "shared/include/flutter_desktop_embedding/method_call.h",
      "shared/include/flutter_desktop_embedding/method_channel.h",
      "shared/include/flutter_desktop_embedding/method_codec.h",
      "shared/include/flutter_desktop_embedding/method_result.h",
      "shared/include/flutter_desktop_embedding/plugin.h",
    ]
  }

  deps = [
    ":fetch_flutter_engine",
  ]

  if (is_linux) {
    public_header_subdir = "flutter_desktop_embedding"

    deps += [
      "//library/linux:publish_flutter_engine",
    ]

    libs = [
      "glfw",
      "GL",
    ]

    configs += [
      "//build/linux/config:epoxy",
      "//build/linux/config:gtk3",
      "//build/linux/config:jsoncpp",
      "//build/linux/config:x11",
    ]

    public_configs = [
      "//library/linux:relative_public_headers",
    ]
  }
}

# Allows targets depending on the engine library to use library-style
# inculdes for its header rather than project-relative.
config("relative_engine_headers") {
  include_dirs = [
    "$engine_download_dir",
  ]
}

action("fetch_flutter_engine") {
  script = "//tools/dart_tools/bin/update_flutter_engine.dart"
  inputs = [ "$flutter_tree_path/bin/internal/engine.version" ]
  outputs = engine_files
  args = [
    "--flutter_root=$flutter_tree_path",
    rebase_path(engine_download_dir, root_build_dir),
  ]
  if (is_linux) {
    public_configs = [
      ":relative_engine_headers",
    ]
  }
}
